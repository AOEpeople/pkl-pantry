/// Similar to `image` property, but will link the specified services to the `image` container.
/// [Learn More](https://docs.gitlab.com/ee/ci/yaml/#services).",
@ModuleInfo { minPklVersion = "0.25.1" }
abstract module com.gitlab.ci.schema.AbstractService

import "gitlab.pkl"

/// Defines variables for a job. [Learn More](https://docs.gitlab.com/ee/ci/yaml/#variables).
typealias Variables = Mapping<String, Boolean|Number|String|VariableSpec>

/// Defines the specification for a job variable value that uses expand.
class VariableSpec {

  /// The variables value
  value: String

  /// Defines if the variable is expandable or not. [Learn More](https://docs.gitlab.com/ee/ci/yaml/#variablesexpand).
  expand: Boolean?
}

/// Job inputs as hidden field to allow configuration of services that extend from this abstract
hidden inputs: Typed?

/// Full name of the image that should be used. It should contain the Registry part if needed.
name: gitlab.ContainerImageName

/// Command or script that should be executed as the container's entrypoint.
/// It will be translated to Docker's --entrypoint option while creating the container.
/// The syntax is similar to Dockerfile's ENTRYPOINT directive, where each shell token
/// is a separate string in the array.
/// [Learn More](https://docs.gitlab.com/ee/ci/services/index.html#available-settings-for-services)",
entrypoint: gitlab.ContainerEntrypoint?

/// Options to pass to Runners Docker Executor.
/// [Learn More](https://docs.gitlab.com/ee/ci/yaml/#imagedocker)
docker: gitlab.ContainerDockerOptions?

/// Specifies how to pull the image in Runner. It can be one of `always`, `never` or `if-not-present`.
/// The default value is `always`. [Learn more](https://docs.gitlab.com/ee/ci/yaml/#imagepull_policy).
pull_policy: gitlab.ContainerImagePullPolicy = "always"

/// Command or script that should be used as the container's command.
/// It will be translated to arguments passed to Docker after the image's name.
/// The syntax is similar to Dockerfile's CMD directive, where each shell token is a separate string in the array.
/// [Learn More](https://docs.gitlab.com/ee/ci/services/index.html#available-settings-for-services)
command: gitlab.ContainerCommand?

/// Additional alias that can be used to access the service from the job's container.
/// Read Accessing the services for more information.
/// [Learn More](https://docs.gitlab.com/ee/ci/services/index.html#available-settings-for-services)
alias: String(length >= 1)?

/// Additional environment variables that are passed exclusively to the service.
/// Service variables cannot reference themselves.
/// [Learn More](https://docs.gitlab.com/ee/ci/services/index.html#available-settings-for-services)
variables: Variables?

/// Function to set name of the image
function setImageName(_image: String) =
  this.toMap().put("name", _image).toTyped(module.getClass())
